! This file is part of Knights.
!
! Copyright (C) Stephen Thompson, 2006 - 2012.
! Copyright (C) Kalle Marjola, 1994.
!
! Knights is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! Knights is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with Knights.  If not, see <http://www.gnu.org/licenses/>.

"Name"     = 'Grammar for Knights Config Files'
"Author"   = 'Stephen Thompson'
"Version"  = '1.0'

"Start Symbol" = <ConfigFile>
"Case Sensitive" = True

! ------------------------------------------------- Sets

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]
{String Chars} = {Printable} + {HT} - ["]   ! printable or tab, but not quote.
{String Chars 2} = {Printable} + {HT} - ['']
{ExponentChar} = [eE]
{DiceChar}     = [dD]
{ID Head Not D} = {ID Head} - {DiceChar}
{HexDigit}     = {Number} + [ABCDEFabcdef]

! ------------------------------------------------- Terminals

Comment Line = '#'

Symbol        = ({ID Head Not D}{ID Tail}*)  |  ({DiceChar} {Number}* {ID Head} {ID Tail}*)  |  {DiceChar}
String        = ('"' {String Chars}* '"')  |  ('' {String Chars 2}* '')
Integer       = {Number}+
HexInteger    = '$' {HexDigit}+
Float         = {Number}* '.' {Number}* ({ExponentChar} '-'? {Number}+)
Dice          = {Number}* {DiceChar} {Number}+

! ------------------------------------------------- Rules

<Assignment> ::= Symbol '=' <Expression>

<Expression> ::= <Expression> '+' <Term>
              |  <Expression> '-' <Term>
              |  <Expression> '&' <Term>
              |  <Term>

<Term> ::= <Term> '*' <Factor>
        |  <Term> '/' <Factor>
        |  <Factor>

<Factor> ::= <Table>
          |  <List>
          |  <Directive> 
          |  Symbol
          |  Integer
          |  HexInteger
          |  Float
          |  String
          |  Dice
          |  '-' <Factor>
          |  '(' <Expression> ')'

<Table> ::= '{' <AssignmentList> '}'

<List> ::= '[' <RepeatList> ']'

<AssignmentList> ::= 
                  |  <AssignmentList> <Assignment>
                  |  <AssignmentList> <Assignment> ','

<RepeatList> ::= 
              |  <RepeatList> <Repeat>
              |  <RepeatList> <Repeat> ','

<ConfigFile> ::= 
              |  <ConfigFile> <Assignment> 
              |  <ConfigFile> <Include> 

<Include> ::= 'include' String

<Repeat> ::= <Expression>
          |  <Expression> ':' Integer

<Directive> ::= Symbol '(' <RepeatList> ')'

